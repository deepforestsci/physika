# ============================================================
# Hamiltonian Neural Network (HNN) in Physika
# ============================================================
# Learning the dynamics of a Simple Harmonic Oscillator
# True Hamiltonian: H(q, p) = (q² + p²) / 2
# Hamilton's equations: dq/dt = ∂H/∂p = p, dp/dt = -∂H/∂q = -q

# ------------------------------------------------------------
# Activation Function
# ------------------------------------------------------------
def tanh(x: ℝ): ℝ:
    return (exp(x) - exp(0.0 - x)) / (exp(x) + exp(0.0 - x))

# ------------------------------------------------------------
# Hamiltonian Network with Physics-Informed Loss
# Input: x = [q, p] ∈ R[2], Output: H ∈ R
# Loss enforces Hamilton's equations in a single expression
# ------------------------------------------------------------
class HamiltonianNet(W1: ℝ[16,2], b1: ℝ[16], w2: ℝ[16], b2: ℝ):
    def λ(x: ℝ[2]) → ℝ:
        return w2 @ tanh(W1 @ x + b1) + b2
    def loss(H: ℝ, target: ℝ[2]) → ℝ:
        return (grad(H, x)[1] - target[0]) ** 2.0 + (0.0 - grad(H, x)[0] - target[1]) ** 2.0

# ------------------------------------------------------------
# Training Data: Simple Harmonic Oscillator
# x = [q, p], target = [dq/dt, dp/dt] = [p, -q]
# ------------------------------------------------------------
X: ℝ[8,2] = [[0.0, 1.0], [1.0, 0.0], [0.0, -1.0], [-1.0, 0.0],
             [0.5, 0.5], [-0.5, -0.5], [0.7, -0.7], [-0.7, 0.7]]
y: ℝ[8,2] = [[1.0, 0.0], [0.0, -1.0], [-1.0, 0.0], [0.0, 1.0],
             [0.5, -0.5], [-0.5, 0.5], [-0.7, -0.7], [0.7, 0.7]]

# ------------------------------------------------------------
# Initialize Hamiltonian Network (weights and bias can be hanfled in backend)
# ------------------------------------------------------------
W1: ℝ[16,2] = [[0.5, 0.1], [0.1, 0.5], [0.3, 0.3], [0.4, 0.2],
               [0.2, 0.4], [0.1, 0.1], [0.3, 0.1], [0.1, 0.3],
               [0.2, 0.2], [0.4, 0.4], [0.5, 0.3], [0.3, 0.5],
               [0.2, 0.1], [0.1, 0.2], [0.4, 0.1], [0.1, 0.4]]
b1: ℝ[16] = [0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0,
             0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0]
w2: ℝ[16] = [0.1, 0.1, 0.1, 0.1, 0.1, 0.1, 0.1, 0.1,
             0.1, 0.1, 0.1, 0.1, 0.1, 0.1, 0.1, 0.1]
b2: ℝ = 0.0

H_net = HamiltonianNet(W1, b1, w2, b2)

# ------------------------------------------------------------
# Evaluate before training
# ------------------------------------------------------------
loss_before = evaluate(H_net, X, y)
loss_before

# ------------------------------------------------------------
# Train HNN using the defined loss in class
# ------------------------------------------------------------

epochs : ℕ = 500
lr : ℝ = 0.01 
H_trained = train(H_net, X, y, epochs, lr)

# ------------------------------------------------------------
# Evaluate after training
# ------------------------------------------------------------
loss_after = evaluate(H_trained, X, y)
loss_after